import java.nio.file.Files
import java.nio.file.Paths
import java.util.zip.ZipFile

// https://stackoverflow.com/a/40624832
def unzipFile(File file) {
	def zipFile = new ZipFile(file)
	zipFile.entries().each { it ->
		def path = Paths.get(file.getParent(), it.name)
		if(it.directory){
			Files.createDirectories(path)
		} else {
			def parentDir = path.getParent()
			if (!Files.exists(parentDir)) {
				Files.createDirectories(parentDir)
			}
			Files.copy(zipFile.getInputStream(it), path)
		}
	}
}

tasks.create("debianClean") {
	doFirst {
		delete(
				Paths.get("build", "distributions", "timecard-kt"),
				Paths.get("build", "debian"),
		)
	}
}

tasks.create("debianPrepare") {
	dependsOn "debianClean"
	doLast {
		unzipFile(new File("build/distributions/timecard-kt.zip"))

		copy {
			from rootProject.files("build/distributions/timecard-kt")
			into rootProject.file("build/debian/timecard-kt")
			exclude("**/*.bat")
		}

		copy {
			from rootProject.file("LICENSE")
			into rootProject.file("build/debian/timecard-kt/debian")
			rename("LICENSE", "copyright")
		}

		copy {
			from rootProject.files("LICENSE", "CHANGELOG.md", "README.md")
			into rootProject.file("build/debian/timecard-kt")
		}

		copy {
			from rootProject.files("debian")
			into rootProject.file("build/debian/timecard-kt/debian")
		}

		// TODO: Create script to convert CHANGELOG.md to Debian changelog
	}
}

tasks.create("debianBuild") {
	dependsOn "debianPrepare"
	doLast {
		exec {
			workingDir rootProject.file("build/debian/timecard-kt")
			commandLine "debuild", "-i", "-us", "-uc", "-b"
		}
	}
}